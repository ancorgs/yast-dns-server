/**
 * File:	include/dns-server/wizards.ycp
 * Package:	Configuration of dns-server
 * Summary:	Wizards definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

    textdomain "dns-server";

    import "Label";
    import "Popup";
    import "Sequencer";
    import "Wizard";

    /**
      * Main workflow of the dns-server configuration
      * @return sequence result
      */
    global define symbol MainSequence() ``{

	map aliases = $[
	    "main"		: ``(runMainDialog ()),
	    "global_options"	: ``(runGlobalOptionsDialog ()),
	    "new_zone"		: ``(runNewZoneDialog ()),
	    "zone_type_switch"	:[``(runZoneTypeSwitch ()), true],
	    "master_zone"	: ``(runMasterZoneDialog ()),
	    "soa"		: ``(runSoaDialog ()),
	    "slave_zone"	: ``(runSlaveZoneDialog ()),
	    "stub_zone"		: ``(runStubZoneDialog ()),
	    "store_zone"	:[``(runZoneStore ()), true],
	    "firewall_settings" : ``(runFirewallDialog ()),
	    "ddns_settings"	: ``(runDDNSDialog ()),
	    "ddns_settings_2"	: ``(runDDNSDialog ()),
	];

	map sequence = $[
	    "ws_start" : "main",
	    "main" : $[
		`abort : `abort,
		`next : `next,
		`add : "new_zone",
		`edit : "zone_type_switch",
		`global_options : "global_options",
		`firewall : "firewall_settings",
		`ddns : "ddns_settings",
	    ],
	    "global_options" : $[
		`abort : `abort,
		`next : "main",
	    ],
	    "firewall_settings" : $[
		`abort : `abort,
		`next : "main",
	    ],
	    "ddns_settings" : $[
		`abort : `abort,
		`next : "main",
	    ],
	    "new_zone" : $[
		`abort : `abort,
		`next : "zone_type_switch",
	    ],
	    "zone_type_switch" : $[
		`abort : `abort,
		`master : "master_zone",
		`slave : "slave_zone",
		`stub : "stub_zone",
	    ],
	    "master_zone" : $[
		`abort : `abort,
		`next : "store_zone",
		`soa : "soa",
		`again : "master_zone",
		`ddns : "ddns_settings_2",
	    ],
	    "ddns_settings_2" : $[
		`abort : `abort,
		`next : "master_zone",
	    ],
	    "soa" : $[
		`abort : `abort,
		`next : "master_zone",
	    ],
	    "slave_zone" : $[
		`abort : `abort,
		`next : "store_zone",
		`again : "slave_zone",
	    ],
	    "stub_zone" : $[
		`abort : `abort,
		`next : "store_zone",
		`again : "stub_zone",
	    ],
	    "store_zone" : $[
		`abort : `abort,
		`next : "main",
	    ],
	];

	symbol ret = Sequencer::Run(aliases, sequence);

	return ret;
    }

    /**
      * Whole configuration of dns-server
      * @return sequence result
      */
    global define symbol DnsSequence() ``{

	map aliases = $[
	    "read"	: [ ``( ReadDialog() ), true ],
	    "main"	:   ``( MainSequence() ),
	    "write"	: [ ``( WriteDialog() ), true ]
	];

	map sequence = $[
	    "ws_start" : "read",
	    "read" : $[
		`abort	: `abort,
		`next	: "main"
	    ],
	    "main" : $[
		`abort	: `abort,
		`next	: "write"
	    ],
	    "write" : $[
		`abort	: `abort,
		`next	: `next
	    ]
	];

	Wizard::CreateDialog();
	Wizard::SetDesktopIcon("dns-server");

	symbol ret = Sequencer::Run(aliases, sequence);

	UI::CloseDialog();
	return ret;
    }

    /**
      * Whole configuration of dns-server
      * @return sequence result
      */
    global define symbol DnsAutoSequence() ``{

	map aliases = $[
	    "main"	:   ``( MainSequence() ),
	];

	map sequence = $[
	    "ws_start" : "main",
	    "main" : $[
		`abort	: `abort,
		`next	: `next
	    ]
	];

	Wizard::CreateDialog();
	Wizard::SetDesktopIcon("dns-server");

	symbol ret = Sequencer::Run(aliases, sequence);

	UI::CloseDialog();
	return ret;
    }

/* EOF */
}
