/**
 * File:	include/dns-server/dialogs.ycp
 * Package:	Configuration of dns-server
 * Summary:	Dialogs definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

    textdomain "dns-server";

    import "DnsServer";

    import "Popup";
    import "Label";
    import "CWM";
    import "Wizard";

    /**
      * Ask user if exit without saving
      * @return boolean true if exit
      */
    global define boolean ReallyExit () ``{
	if (! DnsServer::WasModified ())
	    return true;
	// yes-no popup
	return Popup::YesNo (_("Really exit?
All changes will be lost."));
    }

    /**
      * If modified, ask for confirmation
      * @return true if abort is confirmed
      */
    global define boolean ReallyAbort () ``{
	if (! DnsServer::WasModified ())
	    return true;
	return Popup::ReallyAbort (true);
    }

    /**
      * Ask for confirmation (always)
      * @return true if abort is confirmed
      */
    global define boolean ReallyAbortAlways () ``{
	return Popup::ReallyAbort (true);
    }

    /**
      * Read settings dialog
      * @return `abort if aborted and `next otherwise
      */
    global define symbol ReadDialog() ``{
	Wizard::RestoreHelp (HELPS["read"]:"");
	boolean ret = DnsServer::Read();
	return ret ? `next : `abort;
    }

    /**
      * Write settings dialog
      * @return `abort if aborted and `next otherwise
      */
    global define symbol WriteDialog() ``{
	Wizard::RestoreHelp (HELPS["write"]:"");
	boolean ret = DnsServer::Write();
//	if (! ret && cfg_restart_wanted)
//	    return `back;
	return ret ? `next : `abort;
    }

    /**
      * Run main dialog
      * @return symbol for wizard sequencer
      */
    global define symbol runMainDialog () ``{
	list<map> w = CWM::CreateWidgets (["global_table", "log_button"],
	    DnsServerUI::widgets);
	term contents = `HBox (`HSpacing (1), `VBox (
	    `VSpacing (1),
	    "global_table",
	    `VSpacing (1),
	    "log_button",
	    `VSpacing (1)
	), `HSpacing (1));

	// dialog caption
	string caption = _("DNS Server Configuration");

	string help = CWM::MergeHelps (w);
	contents = CWM::PrepareDialog (contents, w);
	Wizard::SetContentsButtons (caption, contents, help,
	    Label::BackButton (), Label::FinishButton ());
	Wizard::ReplaceBackButton(`VSpacing (0));
	Wizard::ReplaceAbortButton (
	    `PushButton (`id (`abort), Label::CancelButton ()));

	y2error ("Running main dialog");

	return CWM::Run (w,
	     $[`abort : ReallyExit ]);
    }

    /**
      * Run list of zones dialog
      * @return symbol for wizard sequencer
      */
    global define symbol runZonesDialog () ``{
	term contents = `HBox (`HSpacing (1), `VBox (
	    `VSpacing (1),
	    "zones",
	    `VSpacing (1)
	), `HSpacing (1));
	// dialog caption
	string caption = _("Zones Configuration");

	return CWM::ShowAndRun (["zones"], DnsServerUI::widgets,
	    contents, caption, Label::BackButton (), Label::OKButton (),
	     $[`abort : ReallyAbort]);
    }

    /**
      * Run master zone edit dialog
      * @return symbol for wizard sequencer
      */
    global define symbol runMasterZoneDialog () ``{
	term contents = `HBox (`HSpacing (1), `VBox (
	    `VSpacing (1),
	    "master_table",
	    `VSpacing (1)
	), `HSpacing (1));
	// dialog caption
	string caption = _("DNS Master Zone Configuration");

	return CWM::ShowAndRun (["master_table"], DnsServerUI::widgets,
	    contents, caption, Label::BackButton (), Label::OKButton (),
	     $[`abort : ReallyAbortAlways]);
    }

    /**
      * Run slave zone edit dialog
      * @return symbol for wizard sequencer
      */
    global define symbol runSlaveZoneDialog () ``{
	term contents = `HBox (`HSpacing (1), `VBox (
	    `VSpacing (1),
	    "slave_table",
	    `VSpacing (1)
	), `HSpacing (1));
	// dialog caption
	string caption = _("DNS Slave Zone Configuration");

	return CWM::ShowAndRun (["slave_table"], DnsServerUI::widgets,
	    contents, caption, Label::BackButton (), Label::OKButton (),
	     $[`abort : ReallyAbortAlways]);
    }

/* EOF */
}
